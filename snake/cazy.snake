rule extract_gh_hmm:
    output: "ref/hmm/GH{num}.hmm"
    input: "ref/hmm/dbCAN.hmm"
    shell: "hmmfetch {input} GH{wildcards.num} > {output}"

rule extract_reference_gh_domains:
    output: "ref/dbCAN.GH{num}.fa"
    input: "ref/dbCAN.fa"
    shell:
        """
        grep --no-group-separator -A1 "|GH{wildcards.num}$" {input} > {output}
        """

rule extract_mag_gh_domains:
    output: "data/core.a.mags.muri.g.final.GH{num}-domain.fa"
    input: db="data/core.muri.2.denorm.db", fasta="data/core.a.mags.muri.g.final.cds.fa"
    shell:
        """
        seqtk subseq \
                {input.fasta} \
                <(sqlite3 {input.db} "SELECT DISTINCT feature_id FROM feature_x_cazy_domain WHERE domain_id = 'GH{wildcards.num}'") \
                > {output}
        """

rule combine_gh_domains_with_refs:
    output: "data/core.a.mags.muri.g.final.GH{num}-domain.wrefs.fa"
    input: mags="data/core.a.mags.muri.g.final.GH{num}-domain.fa", refs="ref/dbCAN.GH{num}.fa"
    shell: "cat {input.mags} {input.refs} > {output}"

rule align_gh_domains:
    output: "data/core.a.mags.muri.g.final.GH{num}-domain.wrefs.afa"
    input:
        script="scripts/drop_bad_hmmalign.py",
        fasta="data/core.a.mags.muri.g.final.GH{num}-domain.wrefs.fa",
        hmm="ref/hmm/GH{num}.hmm",
    shell:
        """
        tmp=$(mktemp)
        hmmalign --informat fasta --trim {input.hmm} {input.fasta} \
                | convert -f stockholm -t fasta > $tmp
        {input.script} $tmp > {output}
        """
